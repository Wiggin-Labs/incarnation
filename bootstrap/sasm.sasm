;(define Token::LeftParen #'a')
;(define Token::RightParen #'b')
;(define Token::Symbol #'c')
;(define Token::Integer #'d')
;(define Token::Char #'e')
;(define Token::String #'f')
(define Token::LeftParen 0)
(define Token::RightParen 1)
(define Token::Symbol 2)
(define Token::Integer 3)
(define Token::Char 4)
(define Token::String 5)

start
    ;; Check that argc == 2
    (lw x10 x2)
    (addi x11 x0 2)
    (bne x10 x11 error-argc)

    ;; Open input file
    ;; openat(AT_FDCWD, input-name, O_RDONLY, 0)
    (addi x10 x0 -100)
    ;; argv[1]
    (ld x11 (+ x2 16))
    (add x12 x0 x0)
    (add x13 x0 x0)
    (addi x17 x0 56)
    (ecall)
    (blt x10 x0 error-open)

    ;; Read input file to buffer
    ;; mmap(NULL, 64k, PROT_READ, MAP_PRIVATE, fd, 0)
    (add x14 x0 x10)
    (add x10 x0 x0)
    (addi x11 x0 1)
    (slli x11 x11 16)
    (addi x12 x0 1)
    (addi x13 x0 2)
    (add x15 x0 x0)
    (addi x17 x0 222)
    (ecall)
    (blt x10 x0 error-read-input)
    (beq x10 x0 error-read-input)

    (add x31 x0 x10)

    ;; close(fd)
    (add x10 x0 x14)
    (addi x17 x0 57)
    (ecall)

    (add x10 x0 x31)
    (addi x11 x10 -1)
    read-file-length-loop
        (addi x11 x11 1)
        (lb x28 x11)
        (bne x28 x0 read-file-length-loop)

    (sd (- x2 8) x10)
    ;; Tokenize(input, input.len, tokens, tokens.capacity)
    ;; Allocate memory
    ;; Get current end
    ;; brk(0)
    (add x20 x0 x10)
    (add x10 x0 x0)
    (addi x17 x0 214)
    (ecall)
    (add x21 x0 x10)
    ;; 4k
    (addi x22 x0 1)
    (slli x22 x22 12)
    ;; brk(CURRENT_END+4k)
    (add x10 x10 x22)
    (addi x17 x0 214)
    (ecall)
    (add x13 x0 x10)
    (add x10 x0 x20)
    (add x12 x0 x21)

    (jal x1 tokenize)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;; Test tokenizer
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;(add x20 x0 x13)
;(addi x10 x0 1)
;(slli x10 x10 16)
;(add x10 x10 x13)
;(addi x17 x0 214)
;(ecall)
;(add x11 x13 x0)
;(add x18 x0 x12)
;test-tokenizer-loop
;    (beq x18 x19 test-tokenizer-loop-write)
;    (ld x11 (+ 8 x18))
;    (lw x12 (+ 4 x18))
;    (addi x10 x0 1)
;    (addi x17 x0 64)
;    (ecall)
;    (addi x18 x18 16)
;    (jal x0 test-tokenizer-loop)
;test-tokenizer-loop-write
;   ;; write(fd, msg, len)
;    (addi x10 x0 1)
;    ;(add x11 x0 x12)
;    ;(sub x12 x19 x11)
;    (sub x12 x13 x20)
;    (addi x17 x0 64)
;    (ecall)
;    ;; exit(0)
;    (add x10 x0 x0)
;    (addi x17 x0 93)
;    (ecall)
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

    (ld x10 (- x2 8))
    (add x11 x0 x19)
    (jal x1 assemble)
    (jal x1 elf)
    (sd (- 8 x2) x10)
    (sd (- 16 x2) x11)

    ; Get output file name
    (ld x11 (+ x2 16))
    (addi x21 x11 -1)
    (addi x22 x0 #'.')
    find-dot
        (addi x21 x21 1)
        (lb x28 x21)
        (beq x28 x0 load-default-output-name)
        (bne x28 x22 find-dot)
    (sb x21 x0)

    ;; Write to output file
    open-output-name
    ;; openat(AT_FDCWD, output-name, O_WRONLY|O_CREAT|O_TRUNC, 0o777)
    (addi x10 x0 -100)
    (addi x12 x0 577)
    (addi x13 x0 511)
    (addi x17 x0 56)
    (ecall)
    (blt x10 x0 error-open)

    ;; Save fd
    (add x28 x10 x0)
    ;; write(fd, msg, len)
    (ld x11 (- 8 x2))
    (ld x12 (- 16 x2))
    (sub x12 x12 x11)
    (addi x17 x0 64)
    (ecall)

    ;; close(fd)
    (add x10 x28 x0)
    (addi x17 x0 57)
    (ecall)

    ;; exit(0)
    (add x10 x0 x0)
    (addi x17 x0 93)
    (ecall)

    load-default-output-name
        (define default-output-name "bin.elf\0")
        (la x11 default-output-name)
        (jal x0 open-output-name)

    error-argc
        (addi x10 x0 1)
        (jal x0 exit-error)
    error-open
        (addi x10 x0 2)
        (jal x0 exit-error)
    error-read-input
        (addi x10 x0 3)
        (jal x0 exit-error)
    exit-error
        (addi x17 x0 93)
        (ecall)

;; Args:
;;  x10 - input: String
;;  x11 - len: input.len : pointer to end of input
;;  x12 - tokens: ArrayBuf<(Token, len, start)>
;;  x13 - capacity: tokens.capacity : pointer to end of tokens capacity
;; Locals:
;;  x30 - Current token type
;;  x31 - Start of current token
tokenize
    ;; position in tokens
    (add x19 x0 x12)
    tokenize-loop
        (beq x10 x11 tokenize-after-loop)

        ;; Matching char
        (lb x28 x10)
        (add x31 x10 x0)
        (addi x10 x10 1)

        (subi x29 x28 #'(')
        (addi x30 x0 Token::LeftParen)
        (beq x29 x0 tokenize-loop-push-token)

        (subi x29 x28 #')')
        (addi x30 x0 Token::RightParen)
        (beq x29 x0 tokenize-loop-push-token)

        (subi x29 x28 #'"')
        (beq x29 x0 tokenize-string)

        (subi x29 x28 #'#')
        (beq x29 x0 tokenize-literal)

        (subi x29 x28 #';')
        (beq x29 x0 tokenize-comment)

        ;; Skip whitespace
        (subi x29 x28 #' ')
        (beq x29 x0 tokenize-loop)
        (subi x29 x28 #'\t')
        (beq x29 x0 tokenize-loop)
        (subi x29 x28 #'\r')
        (beq x29 x0 tokenize-loop)
        (subi x29 x28 #'\n')
        (beq x29 x0 tokenize-loop)

        ;; Ambiguous, could be a symbol or an integer
        (subi x29 x28 #'0')
        (addi x6 x0 11)
        (bltu x29 x6 tokenize-ambiguous)
        (subi x29 x28 #'+')
        (beq x29 x0 tokenize-ambiguous)
        (subi x29 x28 #'-')
        (beq x29 x0 tokenize-ambiguous)

        (jal x0 tokenize-identifier)

        tokenize-loop-push-token
            (beq x19 x13 tokenize-loop-alloc)
            tokenize-loop-after-alloc
            (sb x19 x30)
            (sd (+ x19 8) x31)
            (sub x31 x10 x31)
            (sw (+ x19 4) x31)
            (addi x19 x19 16)
            (jal x0 tokenize-loop)

            tokenize-loop-alloc
                (add x23 x0 x10)
                ;; 4k
                (addi x22 x0 1)
                (slli x22 x22 12)
                ;; brk(CURRENT_END+4k)
                (add x10 x13 x22)
                (addi x17 x0 214)
                (ecall)
                (add x13 x0 x10)
                (add x10 x0 x23)
                (jal x0 tokenize-loop-after-alloc)

    tokenize-after-loop
        (jalr x0 x1)

    tokenize-string
        (addi x30 x0 Token::String)
    tokenize-string-loop
        (beq x10 x11 tokenize-string-error)
        (lb x28 x10)
        (addi x10 x10 1)

        (subi x29 x28 #'\\')
        (beq x29 x0 tokenize-string-escape)
        (subi x29 x28 #'"')
        (bne x29 x0 tokenize-string-loop)
    tokenize-string-done
        (jal x0 tokenize-loop-push-token)
    tokenize-string-escape
        (beq x10 x11 tokenize-string-error)
        (lb x28 x10)
        (addi x10 x10 1)

        (subi x29 x28 #'r')
        (beq x29 x0 tokenize-string-loop)
        (subi x29 x28 #'n')
        (beq x29 x0 tokenize-string-loop)
        (subi x29 x28 #'t')
        (beq x29 x0 tokenize-string-loop)
        (subi x29 x28 #'0')
        (beq x29 x0 tokenize-string-loop)
        (subi x29 x28 #'\\')
        (beq x29 x0 tokenize-string-loop)
        (subi x29 x28 #'"')
        (beq x29 x0 tokenize-string-loop)
    tokenize-string-error
        (addi x10 x0 20)
        (addi x17 x0 93)
        (ecall)

    tokenize-literal
        (beq x10 x11 tokenize-literal-error)
        (lb x28 x10)
        (addi x10 x10 1)
        (subi x28 x28 #'\'')
        (bne x28 x0 tokenize-literal-error)

        (beq x10 x11 tokenize-literal-error)
        (lb x28 x10)
        (addi x10 x10 1)
        (subi x29 x28 #'\'')
        (beq x29 x0 tokenize-literal-error)
        (subi x29 x28 #'\\')
        (bne x29 x0 tokenize-literal-close)

        (beq x10 x11 tokenize-literal-error)
        (lb x28 x10)
        (addi x10 x10 1)
        (subi x29 x28 #'\\')
        (beq x29 x0 tokenize-literal-close)
        (subi x29 x28 #'\'')
        (beq x29 x0 tokenize-literal-close)
        (subi x29 x28 #'r')
        (beq x29 x0 tokenize-literal-close)
        (subi x29 x28 #'n')
        (beq x29 x0 tokenize-literal-close)
        (subi x29 x28 #'t')
        (beq x29 x0 tokenize-literal-close)
        (subi x29 x28 #'0')
        (beq x29 x0 tokenize-literal-close)

        (jal x0 tokenize-literal-error)
    tokenize-literal-close
        (beq x10 x11 tokenize-literal-error)
        (lb x28 x10)
        (addi x10 x10 1)
        (subi x28 x28 #'\'')
        (bne x28 x0 tokenize-literal-error)
        (addi x30 x0 Token::Char)
        (jal x0 tokenize-loop-push-token)
    tokenize-literal-error
        (addi x10 x0 21)
        (addi x17 x0 93)
        (ecall)

    tokenize-ambiguous
        (beq x10 x11 distinguish-ambiguous)

        (lb x28 x10)
        (addi x10 x10 1)

        ;; Still ambiguous
        (subi x29 x28 #'0')
        (addi x6 x0 11)
        (bltu x29 x6 tokenize-ambiguous)
        (subi x29 x28 #'+')
        (beq x29 x0 tokenize-ambiguous)
        (subi x29 x28 #'-')
        (beq x29 x0 tokenize-ambiguous)


        ;; Delimiter
        (subi x29 x28 #')')
        (beq x29 x0 distinguish-ambiguous-pair)
        (subi x29 x28 #'(')
        (beq x29 x0 distinguish-ambiguous-pair)
        (subi x29 x28 #'#')
        (beq x29 x0 distinguish-ambiguous-pair)
        (subi x29 x28 #';')
        (beq x29 x0 distinguish-ambiguous-pair)
        (subi x29 x28 #'"')
        (beq x29 x0 distinguish-ambiguous-pair)
        (subi x29 x28 #' ')
        (beq x29 x0 distinguish-ambiguous)
        (subi x29 x28 #'\t')
        (beq x29 x0 distinguish-ambiguous)
        (subi x29 x28 #'\r')
        (beq x29 x0 distinguish-ambiguous)
        (subi x29 x28 #'\n')
        (beq x29 x0 distinguish-ambiguous)

        (jal x0 tokenize-identifier)

    ;; There is some other token that we need to account for after this, so we move our cursor back.
    distinguish-ambiguous-pair
        (subi x10 x10 1)
        (add x20 x0 x31)
    distinguish-ambiguous
    distinguish-ambiguous-intp
        (lb x6 x20)
        (subi x7 x6 #'+')
        (beq x7 x0 distinguish-ambiguous-intp-skip-sign)
        (subi x7 x6 #'-')
        (bne x7 x0 distinguish-ambiguous-intp-loop)
        distinguish-ambiguous-intp-skip-sign
            (addi x20 x20 1)
            ;; Needed in case the ambiguous input is just a sign.
            (sub x6 x20 x10)
            (beq x6 x0 distinguish-ambiguous-symbol)

        distinguish-ambiguous-intp-loop
            (lb x6 x20)
            (addi x20 x20 1)
            (subi x6 x6 #'0')
            (addi x7 x0 11)
            (bgeu x29 x7 distinguish-ambiguous-symbol)
            (bne x20 x10 distinguish-ambiguous-intp-loop)
        distinguish-ambiguous-int
            (addi x30 x0 Token::Integer)
            (jal x0 tokenize-loop-push-token)
        distinguish-ambiguous-symbol
            (addi x30 x0 Token::Symbol)
            (jal x0 tokenize-loop-push-token)

    tokenize-identifier
        (addi x30 x0 Token::Symbol)
        tokenize-identifier-loop
            (beq x10 x11 tokenize-loop-push-token)

            (lb x28 x10)
            (addi x10 x10 1)

            ;; delimiters
            (subi x29 x28 #'\r')
            (beq x29 x0 tokenize-identifier-after-loop)
            (subi x29 x28 #'\n')
            (beq x29 x0 tokenize-identifier-after-loop)
            (subi x29 x28 #'\t')
            (beq x29 x0 tokenize-identifier-after-loop)
            (subi x29 x28 #' ')
            (beq x29 x0 tokenize-identifier-after-loop)
            (subi x29 x28 #'#')
            (beq x29 x0 tokenize-identifier-after-loop)
            (subi x29 x28 #'"')
            (beq x29 x0 tokenize-identifier-after-loop)
            (subi x29 x28 #'(')
            (beq x29 x0 tokenize-identifier-after-loop)
            (subi x29 x28 #')')
            (beq x29 x0 tokenize-identifier-after-loop)
            (subi x29 x28 #';')
            (beq x29 x0 tokenize-identifier-after-loop)

            (jal x0 tokenize-identifier-loop)
        tokenize-identifier-after-loop
            (subi x10 x10 1)
            (jal x0 tokenize-loop-push-token)

    tokenize-comment
        (beq x10 x11 tokenize-after-loop)

        (lb x28 x10)
        (addi x10 x10 1)
        (subi x28 x28 #'\n')
        (beq x28 x0 tokenize-loop)
        (jal x0 tokenize-comment)

(define Instr::define "define")
(define Instr::add "add")
(define Instr::sub "sub")
(define Instr::xor "xor")
(define Instr::or "or")
(define Instr::and "and")
(define Instr::sll "sll")
(define Instr::srl "srl")
(define Instr::sra "sra")
(define Instr::slt "slt")
(define Instr::sltu "sltu")

(define Instr::addi "addi")
(define Instr::subi "subi")
(define Instr::xori "xori")
(define Instr::ori "ori")
(define Instr::andi "andi")
(define Instr::slli "slli")
(define Instr::srli "srli")
(define Instr::srai "srai")
(define Instr::slti "slti")
(define Instr::sltiu "sltiu")

(define Instr::lb "lb")
(define Instr::lh "lh")
(define Instr::lw "lw")
(define Instr::ld "ld")
(define Instr::lbu "lbu")
(define Instr::lhu "lhu")
(define Instr::lwu "lwu")
(define Instr::la "la")

(define Instr::sb "sb")
(define Instr::sh "sh")
(define Instr::sw "sw")
(define Instr::sd "sd")

(define Instr::beq "beq")
(define Instr::bne "bne")
(define Instr::blt "blt")
(define Instr::bge "bge")
(define Instr::bltu "bltu")
(define Instr::bgeu "bgeu")

(define Instr::jal "jal")
(define Instr::jalr "jalr")
(define Instr::lui "lui")
(define Instr::auipc "auipc")
(define Instr::ecall "ecall")
(define Instr::ebreak "ebreak")

;; Args:
;;  x10 - input: String
;;  x11 - len: tokens.len
;;  x12 - tokens: ArrayBuf<(Token, len, start)>
;;  x13 - output: ArrayBuf<u32>
;;  x14 - capacity: output.capacity
;; Locals:
assemble
    assemble-loop
        (beq x12 x11 assemble-after-loop)

        (lb x28 x12)
        (addi x12 x12 16)

        (subi x29 x28 Token::Symbol)
        (beq x29 x0 assemble-add-label)
        (subi x29 x28 Token::LeftParen)
        (bne x29 x0 assemble-error)

        (lb x28 x12)
        (subi x29 x28 Token::Symbol)
        (bne x29 x0 assemble-error)
        (ld x28 (+ x12 8))
        (lw x29 (+ x12 4))
        (addi x12 x12 16)
        (la x30 Instr::define)
        (addi x31 x0 6)
        (jal x2 assemble-str-eq)
        (beq x30 x0 assemble-handle-opcode)
    assemble-handle-define
        ;; TODO
    assemble-add-label
        ;; TODO
    assemble-handle-opcode
        (la x30 Instr::add)
        (addi x31 x0 3)
        (jal x2 assemble-str-eq)
        ;; TODO
        (bne x30 x0 assemble-r)
    assemble-r
    assemble-unwrap-register
        (lb x6 x12)
        (subi x6 x6 Token::Symbol)
        (bne x6 x0 assemble-error)
        (ld x6 (+ x12 8))
        (lw x7 (+ x12 4))
        (addi x12 x12 16)
    assemble-str-eq
        (bne x29 x30 assemble-str-eq-false)
        assemble-str-eq-loop
            (beq x29 x0 assemble-str-eq-true)
            (subi x29 x29 1)
            (lb x6 x28)
            (addi x28 x28 1)
            (lb x7 x30)
            (addi x30 x30 1)
            (beq x6 x7 assemble-str-eq-loop)
        assemble-str-eq-false
            (add x30 x0 x0)
            (jalr x0 x2)
        assemble-str-eq-true
            (addi x30 x0 1)
            (jalr x0 x2)
    assemble-after-loop
        (jalr x0 x1)
    assemble-error
        (addi x10 x0 30)
        (addi x17 x0 93)
        (ecall)

;; Based on simple design outlined near the beginning of https://moss.cs.iit.edu/cs351/slides/slides-malloc.pdf
;; x10 - size in bytes
(define Malloc::Alignment 7)
(define Malloc::SIZE_T_SIZE 8)
malloc
    ;; Align(size)
    (addi x6 x0 Malloc::Alignment)
    (add x10 x10 x6)
    (xori x6 x6 -1)
    (and x10 x10 x6)

    ;; header
    (ori x6 x10 1)
    ;; sbrk(blk_size)
    ;; brk(0) => CURRENT_END
    (add x7 x0 x10)
    (add x10 x0 x0)
    (addi x17 x0 214)
    (ecall)
    ;; brk(CURRENT_END+blk_size)
    (add x10 x10 x7)
    (addi x17 x0 214)
    (ecall)

    (sb x10 x6)
    ;; SIZE_T_SIZE = 8
    (addi x10 x10 Malloc::SIZE_T_SIZE)
    (jalr x0 x1)

;; x10 - pointer to free
free
    ;; SIZE_T_SIZE = 8
    (subi x10 x10 Malloc::SIZE_T_SIZE)
    (lb x6 x10)
    (addi x7 x0 1)
    (xori x7 x7 -1)
    (and x6 x6 x7)
    (sd x10 x6)
    (jalr x0 x1)

;; x10 - old pointer
;; x11 - new size in bytes
realloc
    (subi x2 x2 16)
    (sd (+ x2 8) x10)
    (sd x2 x1)
    (add x10 x11 x0)
    (jal x1 malloc)
    (ld x1 x2)
    (ld x11 (+ x2 8))
    (addi x2 x2 16)
    (add x6 x0 x10)

    ;; Load the header/length of the old block
    (ld x7 (- x11 8))
    (subi x7 x6 1)
    realloc-copy-loop
        ;; Note we load a u64 at a time to save on iterations.
        (ld x8 x11)
        (sd x6 x8)
        (addi x6 x6 8)
        (addi x11 x11 8)
        (subi x7 x7 1)
        (bne x7 x0 realloc-copy-loop)
    realloc-after-loop
        (jalr x0 x1)

;; x10 - &program
;; x11 - program.len
;; x12 - &data (len, &data)
;; x13 - data.len
;; x14 - &rewrites (program_index, data_index)
;; x15 - rewrites.len
;; Locals
;;   x20 - elf output buffer
;;   x21 - data_offset
;;   x22 - data_len
;;   x23 - shstrtab_offset
;;   x24 - &data_position (offset)
;;   x25 - elf output buffer beginning
elf
    (define Elf::shstrtab "\0.text\0.data\0.shstrtab\0")
    (define Elf::shstrtab-len 23)
    ;; 0x600000
    (define Elf::DATA_LOCATION 6291456)
    ;; 64 + 56 + 56
    (define Elf::data_offset 176)

    ;; Allocate 64k for exe output
    (sd (- 8 x2) x10)
    ;; brk(0)
    (add x10 x0 x0)
    (addi x17 x0 214)
    (ecall)
    (add x20 x0 x10)
    (add x25 x0 x10)
    ;; 64k
    (addi x22 x0 1)
    (slli x22 x22 16)
    ;; brk(CURRENT_END+6k)
    (add x10 x10 x22)
    (addi x17 x0 214)
    (ecall)
    (ld x10 (- 8 x2))

    (sub x21 x11 x10)
    (addi x21 x21 Elf::data_offset)

    (add x28 x0 x13)
    (sub x31 x2 x28)
    (sub x24 x2 x28)
    (add x29 x0 x12)
    (add x22 x0 x0)
    (beq x28 x0 elf-get-data-len-loop-after)
    ;; 0x600000
    (addi x6 x0 6)
    (slli x6 x6 20)
    (add x6 x21 x6)
    elf-get-data-len-loop
        (ld x30 x29)
        (add x22 x22 x30)
        (addi x29 x29 16)
        (subi x13 x0 1)
        (sd x31 x6)
        (add x6 x6 x30)
        (addi x31 x31 8)
        (bne x28 x0 elf-get-data-len-loop)
    elf-get-data-len-loop-after
    (add x23 x21 x30)

    elf-rewrites
        (beq x15 x0 elf-write-ehdr)
        (ld x28 x14)
        (add x28 x28 x10)
        (ld x29 (+ x14 8))
        (add x6 x0 x24)
        elf-rewrites-mult
            (beq x29 x0 elf-rewrites-mult-after)
            (addi x6 x6 8)
            (subi x29 x29 1)
            (jal x0 elf-rewrites-mult)
        elf-rewrites-mult-after
        ;; offset
        (ld x6 x6)
        ;; lui
        (lw x30 x28)
        ;; 0xff_ff_f0_00
        ;(addi x7 x0 65535)
        ;(slli x7 x7 16)
        ;(addi x7 x7 61440)
        ;(and x7 x6 x7)
        (srai x7 x6 12)
        (slli x7 x7 12)
        (or x30 x30 x7)
        (sw x28 x30)
        ;; addi
        (lw x30 (+ x28 4))
        ;; 0xf_ff
        (addi x7 x0 15)
        (slli x7 x7 8)
        (addi x7 x7 255)
        (and x7 x6 x7)
        (slli x7 x7 20)
        (or x30 x30 x7)
        (sw (+ x28 4) x30)
        (addi x14 x14 16)
        (subi x15 x15 1)
        (jal x0 elf-rewrites)

    elf-write-ehdr
        ;; 0x7f
        (addi x31 x0 127)
        (sb x20 x31)
        (addi x31 x0 #'E')
        (sb (+ 1 x20) x31)
        (addi x31 x0 #'L')
        (sb (+ 2 x20) x31)
        (addi x31 x0 #'F')
        (sb (+ 3 x20) x31)
        (addi x31 x0 2)
        (sb (+ 4 x20) x31) ;; 1 for 32 bit, 2 for 64 bit
        (addi x31 x0 1)
        (sb (+ 5 x20) x31) ;; endianness - 1 is little-endian
        (sb (+ 6 x20) x31) ;; version
        (sb (+ 7 x20) x0) ;; abi version
        (sd (+ 8 x20) x0)
        (addi x20 x20 16)

        ;; e_type
        (addi x31 x0 2)
        (sh x20 x31)
        ;; e_machine
        ;; RISCV = 0xf3
        (addi x31 x0 243)
        (sh (+ 2 x20) x31)
        ;; e_version
        (addi x31 x0 1)
        (sw (+ 4 x20) x31)
        ;; e_entry
        ;; 0x4000b0
        (addi x31 x0 4)
        (slli x31 x31 20)
        (addi x31 x31 176)
        (sd (+ 8 x20) x31)
        ;; e_phoff
        (addi x31 x0 64)
        (sd (+ 16 x20) x31)
        ;; e_shoff
        (addi x31 x23 Elf::shstrtab-len)
        (sd (+ 24 x20) x31)
        ;; e_flags
        (sw (+ 32 x20) x0)
        ;; e_ehsize
        (addi x31 x0 64)
        (sh (+ 36 x20) x31)
        ;; e_phentsize
        (addi x31 x0 56)
        (sh (+ 38 x20) x31)
        ;; e_phnum
        (addi x31 x0 2)
        (sh (+ 40 x20) x31)
        ;; e_shentsize
        (addi x31 x0 64)
        (sh (+ 42 x20) x31)
        ;; e_shnum
        (addi x31 x0 4)
        (sh (+ 44 x20) x31)
        ;; e_shstrndx
        (addi x31 x0 3)
        (sh (+ 46 x20) x31)
        (addi x20 x20 48)

    elf-write-phdrs
        elf-write-phdrs-text
            ;; p_type
            (addi x31 x0 1)
            (sw x20 x31)
            ;; p_flags
            (addi x31 x0 5)
            (sw (+ 4 x20) x31)
            ;; p_offset
            (sd (+ 8 x20) x0)
            ;; p_vaddr
            ;; 0x400000
            (addi x31 x0 4)
            (slli x31 x31 20)
            (sd (+ 16 x20) x31)
            ;; p_paddr
            ;; 0x400000
            (sd (+ 24 x20) x31)
            ;; p_filesz
            (sd (+ 32 x20) x21)
            ;; p_memsz
            (sd (+ 40 x20) x21)
            ;; p_align
            ;; 0x200000
            (addi x31 x0 2)
            (slli x31 x31 20)
            (sd (+ 48 x20) x31)
            (addi x20 x20 56)
        elf-write-phdrs-data
            ;; p_type
            (addi x31 x0 1)
            (sw x20 x31)
            ;; p_flags
            (addi x31 x0 6)
            (sw (+ 4 x20) x31)
            ;; p_offset
            (sd (+ 8 x20) x21)
            ;; p_vaddr
            ;; 0x600000
            (addi x31 x0 6)
            (slli x31 x31 20)
            (add x31 x21 x31)
            (sd (+ 16 x20) x31)
            ;; p_paddr
            ;; 0x600000
            (sd (+ 24 x20) x31)
            ;; p_filesz
            (sd (+ 32 x20) x22)
            ;; p_memsz
            (sd (+ 40 x20) x22)
            ;; p_align
            ;; 0x200000
            (addi x31 x0 2)
            (slli x31 x31 20)
            (sd (+ 48 x20) x31)
            (addi x20 x20 56)
    elf-write-shstrtab
        (la x6 Elf::shstrtab)
        (addi x7 x6 Elf::shstrtab-len)
    elf-write-shstrtab-loop
        (lb x8 x6)
        (sb x20 x8)
        (addi x6 x6 1)
        (addi x20 x20 1)
        (bne x6 x7 elf-write-shstrtab-loop)
    elf-write-s_hdrs
        elf-write-s_hdrs-null
            ;; sh_name
            (sw x20 x0)
            ;; sh_type
            (sw (+ 4 x20) x0)
            ;; sh_flags
            (sd (+ 8 x20) x0)
            ;; sh_addr
            (sd (+ 16 x20) x0)
            ;; sh_offset
            (sd (+ 24 x20) x0)
            ;; sh_size
            (sd (+ 32 x20) x0)
            ;; sh_link
            (sd (+ 40 x20) x0)
            ;; sh_info
            (sd (+ 44 x20) x0)
            ;; sh_addralign
            (sd (+ 48 x20) x0)
            ;; sh_entsize
            (sd (+ 56 x20) x0)
            (addi x20 x20 64)
        elf-write-s_hdrs-text
            ;; sh_name
            (addi x31 x0 1)
            (sw x20 x31)
            ;; sh_type
            (sw (+ 4 x20) x31)
            ;; sh_flags
            (addi x31 x0 6)
            (sd (+ 8 x20) x31)
            ;; sh_addr
            ;; 0x4000b0
            (addi x31 x0 1024)
            (slli x31 x31 12)
            (addi x31 x31 176)
            (sd (+ 16 x20) x31)
            ;; sh_offset
            ;; 0xb0
            (addi x31 x0 176)
            (sd (+ 24 x20) x31)
            ;; sh_size
            (sd (+ 32 x20) x11)
            ;; sh_link
            (sd (+ 40 x20) x0)
            ;; sh_info
            (sd (+ 44 x20) x0)
            ;; sh_addralign
            (addi x31 x0 16)
            (sd (+ 48 x20) x31)
            ;; sh_entsize
            (sd (+ 56 x20) x0)
            (addi x20 x20 64)
        elf-write-s_hdrs-data
            ;; sh_name
            (addi x31 x0 7)
            (sw x20 x31)
            ;; sh_type
            (addi x31 x0 1)
            (sw (+ 4 x20) x31)
            ;; sh_flags
            (addi x31 x0 3)
            (sd (+ 8 x20) x31)
            ;; sh_addr
            ;; 0x600000
            (addi x31 x0 6)
            (slli x31 x31 20)
            (add x31 x21 x31)
            (sd (+ 16 x20) x31)
            ;; sh_offset
            (sd (+ 24 x20) x21)
            ;; sh_size
            (sd (+ 32 x20) x22)
            ;; sh_link
            (sd (+ 40 x20) x0)
            ;; sh_info
            (sd (+ 44 x20) x0)
            ;; sh_addralign
            (addi x31 x0 4)
            (sd (+ 48 x20) x31)
            ;; sh_entsize
            (sd (+ 56 x20) x0)
            (addi x20 x20 64)
        elf-write-s_hdrs-shstrtab
            ;; sh_name
            (addi x31 x0 13)
            (sw x20 x31)
            ;; sh_type
            (addi x31 x0 3)
            (sw (+ 4 x20) x31)
            ;; sh_flags
            (sd (+ 8 x20) x0)
            ;; sh_addr
            (sd (+ 16 x20) x0)
            ;; sh_offset
            (sd (+ 24 x20) x23)
            ;; sh_size
            (addi x31 x0 Elf::shstrtab-len)
            (sd (+ 32 x20) x31)
            ;; sh_link
            (sd (+ 40 x20) x0)
            ;; sh_info
            (sd (+ 44 x20) x0)
            ;; sh_addralign
            (addi x31 x0 1)
            (sd (+ 48 x20) x31)
            ;; sh_entsize
            (sd (+ 56 x20) x0)
            (addi x20 x20 64)
    elf-write-data
        (beq x13 x0 elf-write-program)
        (ld x28 x12)
        (ld x29 (+ 8 x12))
        elf-write-data-inner
            (lb x30 x29)
            (sb x20 x30)
            (addi x20 x20 1)
            (addi x29 x29 1)
            (subi x28 x28 1)
            (bne x28 x0 elf-write-data-inner)
        (addi x12 x12 16)
        (subi x13 x13 1)
        (jal x0 elf-write-data)
    elf-write-program
        (lw x6 x10)
        (sw x20 x6)
        (addi x10 x10 4)
        (addi x20 x20 4)
        (bne x10 x11 elf-write-program)
    elf-after
        (add x11 x0 x25)
        (add x11 x0 x20)
        (jalr x0 x1)
